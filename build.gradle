plugins {
    id 'java'
    id 'com.palantir.docker' version '0.22.1'
    id 'jp.classmethod.aws.ec2' version '0.41'
    id 'net.researchgate.release' version '2.8.1'
    id 'org.springframework.boot' version '2.1.5.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

test {
    useTestNG()
    testLogging {
        showStandardStreams = true
    }
}

bootJar {
    mainClassName = 'uk.co.automatictester.simple.app.Application'
    manifest {
        attributes 'Implementation-Version': archiveVersion
    }
}

docker {
    dependsOn dockerPrepare
    files "build/libs/${project.name}-${project.version}.jar"
    name "${aws.accountId}.dkr.ecr.eu-west-2.amazonaws.com/${project.name}:${project.version}"
}

release {
    buildTasks = ['generateBambooArtifact', 'jar', 'dockerPush']
    git {
        requireBranch = 'master'
    }
}

task generateBambooArtifact {
    new File('version.properties').text = "version=${version}"
}

repositories {
    mavenCentral()
}

def lombokVersion = '1.18.12'

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.jtwig:jtwig-spring-boot-starter:5.87.0.RELEASE'
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.testng:testng:7.1.0'
    testImplementation 'org.assertj:assertj-core:3.15.0'
}
